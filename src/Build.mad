import IO from "IO"
import { andDo } from "Monad"
import { Wish, bad, fulfill, mapRej, parallel } from "Wish"
import { fromMaybe, Just, Nothing } from "Maybe"
import { nth, drop, intercalate, last, reduce } from "List"
import { split } from "String"
import {
  exec,
  getCurrentPath,
  getExecutablePath,
  CommandError,
  getCommandErrorMessage
} from "System"

import { IOUtils, defaultIOUtils } from "./IOUtils"

alias InputOutput = { input :: String, output :: String }

export alias Config = {
  madlib               :: InputOutput,
  styles               :: InputOutput,
  html                 :: InputOutput,
  sassExecutablePath   :: String,
  documentationDotJson :: InputOutput
}

MADDOC_PACKAGE_FOLDER :: String
MADDOC_PACKAGE_FOLDER = "https___github_com_madlib_lang_maddoc_archive_refs_heads_master_zip"

MADLIB_MODULES_FOLDER :: String
MADLIB_MODULES_FOLDER = "madlib_modules"

JS_BUNDLE_PATH :: String
JS_BUNDLE_PATH = ".docs/bundle.js"

CSS_TARGET_PATH :: String
CSS_TARGET_PATH = ".docs/styles/main.css"

HTML_TARGET_PATH :: String
HTML_TARGET_PATH = ".docs/index.html"


parentPath :: Number -> String -> String
parentPath = (levelsUp) => pipe(
  split("/"),
  drop(levelsUp),
  intercalate("/"),
  reduce(mappend, "")
)

pathToDocument :: IOUtils -> Wish String String
export pathToDocument = pipe(
  .getArgs,
  map(nth(0)),
  chain(where
    is Just path: of(path)
    is Nothing  : bad("You must give a path to the .mad files to document!")
  )
)


madDocRootPath :: IOUtils -> Wish String String
export madDocRootPath = (ioUtils) => map(
  (path) => pipe(
    parentPath(3),
    split("/"),
    last,
    where
      is Just "madlib_modules": `${parentPath(3, path)}/${MADDOC_PACKAGE_FOLDER}`
      is _                    : `${parentPath(2, path)}/${MADLIB_MODULES_FOLDER}/${MADDOC_PACKAGE_FOLDER}`
  )(path),
  ioUtils.getExecutablePath
)

sassExecutablePath :: IOUtils -> Wish String String
export sassExecutablePath = (ioUtils) => map(
  (rootPath) => `${parentPath(2, rootPath)}/node_modules/.bin/sass`,
  madDocRootPath(ioUtils)
)

madDocSrcPath :: IOUtils -> Wish String String
export madDocSrcPath = (ioUtils) => map(
  mappend($, "/src"),
  madDocRootPath(ioUtils)
)

documentationTargetPath :: IOUtils -> Wish String String
export documentationTargetPath = (ioUtils) => map(
  mappend($, "/documentation.json"),
  madDocSrcPath(ioUtils)
)

madDocMainPath :: IOUtils -> Wish String String
export madDocMainPath = (ioUtils) => map(
  mappend($, "/Main.mad"),
  madDocSrcPath(ioUtils)
)

sassPath :: IOUtils -> Wish String String
sassPath = (ioUtils) => map(
  mappend($, "/styles/main.scss"),
  madDocSrcPath(ioUtils)
)

htmlSourcePath :: Wish String String
htmlSourcePath = map(mappend($, "/index.html"), madDocSrcPath(defaultIOUtils))


generateConfiguration :: Wish String Config
export generateConfiguration = pipe(
  parallel,
  map(where is [madMain, sassMain, htmlIndex, sassBin, docInput, docOutput]: {
    madlib: { input: madMain, output: JS_BUNDLE_PATH },
    styles: { input: sassMain, output: CSS_TARGET_PATH },
    html: { input: htmlIndex, output: HTML_TARGET_PATH },
    sassExecutablePath: sassBin,
    documentationDotJson: { input: docInput, output: docOutput }
  })
)([
  madDocMainPath(defaultIOUtils),
  sassPath(defaultIOUtils),
  htmlSourcePath,
  sassExecutablePath(defaultIOUtils),
  pathToDocument(defaultIOUtils),
  documentationTargetPath(defaultIOUtils)
])


generateJson :: Config -> Wish String String
export generateJson = (config) => pipe(
  (c) => `madlib doc -i ${c.documentationDotJson.input} > ${c.documentationDotJson.output}`,
  exec,
  mapRej(getCommandErrorMessage),
  mapRej((err) =>
    `An error occured while generating the docs, here is the error from madlib:\n${err}`
  ),
  map((_) => `Documentation built and saved in '${config.documentationDotJson.output}'`)
)(config)


buildBundle :: Config -> Wish String String
buildBundle = pipe(
  (c) => exec(`madlib compile -i ${c.madlib.input} --target browser --bundle -o ${c.madlib.output}`),
  mapRej(getCommandErrorMessage)
)

buildSass :: Config -> Wish String String
buildSass = pipe(
  (c) => exec(`${c.sassExecutablePath} ${c.styles.input} ${c.styles.output}`),
  mapRej(getCommandErrorMessage)
)

copyHtml :: Config -> Wish String String
copyHtml = pipe(
  (c) => exec(`cp ${c.html.input} ${c.html.output}`),
  mapRej(getCommandErrorMessage)
)

buildDocumentation :: Config -> Wish String String
export buildDocumentation = (config) => pipe(
  buildBundle,
  andDo(buildSass(config)),
  andDo(copyHtml(config)),
  andDo(map(mappend($, "/.docs"), getCurrentPath)),
  map((outputPath) => `Documentation built and available in the folder '${outputPath}'`)
)(config)
